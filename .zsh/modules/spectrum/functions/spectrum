#
# Print a spectrum
#
# Usage: spectrum [columns]

if [[ $1 != <-> && "$1" != '' ]]; then
  print "Invalid argument '$1'"
  print "Usage: $0 [columns]"
  return 1
fi

local i line space color=1

# Color preview
for i in {0..15}; do space="$space "; done

# Number of columns to display the spectrum
local columns=${1:-1}

# Represents each line of output with $columns colors each
local -a rows

# Total lines of output. We use 255 because there's no need to print color 0,
# the background
local nlines=$(( 255 / columns))
(( 255 % columns )) && (( ++nlines ))

for line in {1..$nlines}; do
  for i in {1..$columns}; do
    (( color > 255 )) && break;
    rows[$line]="${rows[$line]}  %{$FG[$color]%}#$(printf '%03d' $color)%{$FX[reset]%} %{$BG[$color]%}$space%{$FX[reset]%}"
    (( ++color ))
  done
  print -P "$rows[$line]"
done

# vim: ft=zsh
